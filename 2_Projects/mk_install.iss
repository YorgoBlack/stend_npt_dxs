; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
;#include "..\..\3_Source\ver.h"
#define MyAppPublisher "ООО ПО ОВЕН , 2007-2018 г."
#define MyAppURL "http://owen.ru/"
#define BaseDir "..\3_Source\npt_stenddxs_multidevice\npt_stenddxs_multidevice\bin\Release\"
#define MyAppName "Программа калибровки ДХС"
#define MainBinaryName  "npt_stenddxs_multidevice.exe"
#define SetupBaseName   "Setup_npt_stenddxs_multidevice_v"
#define AppVersion      GetFileVersion(AddBackslash(BaseDir) + MainBinaryName)
 
[Setup]
AppName={#MyAppName}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
AppVersion={code:GetVer}
DefaultDirName={code:SetupPathVar}
DefaultGroupName={code:GetGroupVar}
PrivilegesRequired=admin
ArchitecturesInstallIn64BitMode=x64
UsePreviousLanguage=no
OutputDir=..\5_Release\
OutputBaseFilename={#SetupBaseName + AppVersion}
Compression=bzip/9
SolidCompression=true
DisableStartupPrompt=true
DisableProgramGroupPage=true
DisableWelcomePage=no
DisableDirPage=no

[Languages]
Name: russian; MessagesFile: compiler:Languages\Russian.isl

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: checkedonce; 

[Files]
Source: {#BaseDir}{#MainBinaryName}; DestDir: {app}; Flags: ignoreversion; BeforeInstall: CleanSettings
Source: {#BaseDir}Xceed.Wpf.Toolkit.dll; DestDir: {app}; Flags: ignoreversion
Source: {#BaseDir}Xceed.Wpf.DataGrid.dll; DestDir: {app}; Flags: ignoreversion
Source: {#BaseDir}Xceed.Wpf.AvalonDock.Themes.VS2010.dll; DestDir: {app}; Flags: ignoreversion
Source: {#BaseDir}Xceed.Wpf.AvalonDock.Themes.Metro.dll; DestDir: {app}; Flags: ignoreversion
Source: {#BaseDir}Xceed.Wpf.AvalonDock.Themes.Aero.dll; DestDir: {app}; Flags: ignoreversion
Source: {#BaseDir}Xceed.Wpf.AvalonDock.dll; DestDir: {app}; Flags: ignoreversion
Source: {#BaseDir}System.Windows.Interactivity.dll; DestDir: {app}; Flags: ignoreversion
Source: {#BaseDir}MahApps.Metro.dll; DestDir: {app}; Flags: ignoreversion
Source: {#BaseDir}ControlzEx.dll; DestDir: {app}; Flags: ignoreversion
Source: {#BaseDir}Cet.IO_NET45.dll; DestDir: {app}; Flags: ignoreversion
Source: {#BaseDir}Cet.IO.Serial_NET45.dll; DestDir: {app}; Flags: ignoreversion
Source: {#BaseDir}Cet.IO.Protocols.Modbus_NET45.dll; DestDir: {app}; Flags: ignoreversion
Source: {#BaseDir}Cet.IO.Net_NET45.dll; DestDir: {app}; Flags: ignoreversion
Source: {#BaseDir}SensorData\*; DestDir: {app}\SensorData; Flags: ignoreversion recursesubdirs
[Icons]
Name: {group}\Программа калибровки ДХС; Filename: {app}\{#MainBinaryName};
Name: {group}\{cm:UninstallProgram,{#MyAppName}}; Filename: {uninstallexe}; WorkingDir: {app}\uninstall
Name: {commondesktop}\{#MyAppName}; Filename: {app}\{#MainBinaryName}; WorkingDir: {app}; Tasks: desktopicon

[Run]
Filename: {app}\{#MainBinaryName}; Description: {cm:LaunchProgram,{#MyAppName}}; Flags: nowait postinstall skipifsilent

[Code]

var
  AppFolder: String;
  strGroup: String ;


function GetGroupVar(Param: String): String;
begin
  Result:=strGroup;
end;

function SetupPathVar(Param: String): String;
  var
  strPath: String;
begin
    // Ставим значение по умолчанию...
      Result := ExpandConstant('{pf}\Owen\{#MyAppName}');
      strGroup:='Owen\{#MyAppName}';
end;


function GetVer(Param: String): String;
var
  Ver : String;
begin
    GetVersionNumbersString('{#BaseDir}' + '{#MainBinaryName}', Ver);
    Result := Ver;
end;

procedure CleanSettings();
begin
    DelTree(ExpandConstant('{userappdata}\OWEN\npt_stenddxs_multidevice'), True, True, True);
    RemoveDir(ExpandConstant('{userappdata}\OWEN\npt_stenddxs_multidevice'));
end;

